import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id "java-library"
    id("com.vanniktech.maven.publish.base") version "0.29.0"
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    pomFromGradleProperties()
    signAllPublications()
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact "build/dist/android/avs.aar"
                pom {
                    name.set("wire-avs")
                    description.set("Wire - Audio, Video, and Signaling (AVS)")
                    url.set("https://github.com/wireapp/wire-avs")
                    licenses {
                        license {
                            name.set("GPL-3.0")
                            url.set("https://opensource.org/licenses/GPL-3.0")
                        }
                    }
                    scm {
                        connection.set("scm:git:git://github.com/wireapp/wire-avs")
                        developerConnection.set("scm:git:git://github.com/wireapp/wire-avs")
                        url.set("https://github.com/wireapp/wire-avs")
                    }
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')
                        findProject(':android:lib').configurations.implementation.allDependencies.each {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
    // Allows skipping signing jars published to 'MavenLocal' repository
    tasks.named("signMavenJavaPublication").configure {
        if (System.getenv("CI") == null) { // i.e. not in Github Action runner
            enabled = false
        }
    }
}